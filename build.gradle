buildscript {
    repositories {
        jcenter() // Shadow plugin
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
    }
}

defaultTasks 'clean', 'build', 'publish'

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow' // Shadow jar

    group 'com.alessiodp.lastloginapi'
    version '1.1.0'

    // Info
    ext.pluginName = "LastLoginAPI"
    ext.pluginDescription = "A library to handle player names and login timestamps"
    ext.pluginAuthor = "AlessioDP"
    ext.pluginWebsite = "https://alessiodp.com/"

    // Libraries
    ext.placeholderApi = '2.9.2'

    // Development
    ext.adpCore = '1.1.0'
    ext.lombok = '1.18.6'
    ext.powermock = '2.0.0'

    // Misc
    sourceCompatibility = 1.8

    repositories {
        mavenLocal()

        // Maven central
        mavenCentral()
        // CodeMC repository - ADP Core
        maven { url = 'https://repo.codemc.org/repository/maven-public/' }
        // Spigot repository for Spigot & Bukkit
        maven { url = 'https://hub.spigotmc.org/nexus/content/groups/public/' }
        // OSS Sonatype repository for Bungeecord
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
        // JitPack repo
        maven { url = 'https://jitpack.io' }
    }

    dependencies {
        compileOnly group: 'org.projectlombok', name: 'lombok', version: project.ext.lombok
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: project.ext.lombok

        testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: project.ext.powermock
        testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: project.ext.powermock
    }
}

dependencies {
    compile project(':lastloginapi-api')
    compile project(':common')
    compile project(':bukkit')
    compile project(':bungeecord')
}

// Disable root project jar
jar.enabled = false

shadowJar {
    archiveBaseName  = project.ext.pluginName
    archiveClassifier = null
    dependencies {
        include(dependency('com.alessiodp.lastloginapi:.*'))
        include(dependency('com.alessiodp.core:.*'))
    }

    relocate 'com.alessiodp.core', 'com.alessiodp.lastloginapi.core'
}

artifacts {
    archives shadowJar
}
